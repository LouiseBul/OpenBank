require: slotfilling/slotFilling.sc
  module = sys.zb-common
  
patterns:
    $change = (смен*|измен*|поменя*|поменч*|*менять|перемен*|*исмен*|замен*|прави*|поправ*|исправ*|сброс*|зброс*|скину*)
    $lost = (потеря*|утеря*|затеря*|не [$oneWord] *помн*|забыт*|*забыл*)
    $restore = (вспомни*|вспомина*|востанов*|восстанов*|восмтанов*)
    $create = (сдела*|здела*|установ*|созда*)
    $password = (парол*|код*|кот*|пин*|pin*|пинкод*|пинккод*|репин*|пин-код*|пин-кот*|pin-код*|pin-cod*)
    $err = (неправил*|неверн*|не (правил*|верн*)|ошибк*|ошибок*|ошибоч*)
    $app = (прог*|прилог*|прилож*|систем*|лк|[личн*] кабинет*|профил*|онлайн* банк*|онлайн-банк*|онлайнбанк*)
    $findOut = (*смотре*|посмотре*|узна*|выясн*|найт*|наход*|отиска*|отыска*)
    
theme: /

    state: Change password
        q!: * $password *
        q: * {($change|$lost|$restore|$create|$err|проблем*) * $password} *
        q: * {[как*|где] * ($findOut|ввест*|ввод*|запис*|запиш*|выда*|получ*|провер*|управл*|сохран*|хранит*|хранят*) * $password} *
        a: Здравствуйте.
        go!: /Specify

    state: Specify
        a: Сейчас расскажу порядок действий.
            Выберите, что именно планируете сделать:
            1. Поменять пароль для входа в приложение.
            2. Поменять PIN-код от карты.
            Пожалуйста, отправьте цифру, соответствующую вашему выбору.
            
    state: App
        q: [$oneWord] (1*|перв*|один*|един*|$app) [$oneWord] || fromState = "/Specify"
        q: * {$password * $app} *
        q: * {$password * (вход*|вхожд*|доступ*|зайт*|зайд*) * [$app]} *
        q: * {($change|$lost|$restore|$create|$err|проблем*) * $password * ($app|вход*|доступ*)} *
        q: * {{[как*|где] * ($findOut|ввест*|ввод*|запис*|запиш*|выда*|получ*|провер*|управл*|сохран*|хранит*|хранят*)} * $password * $app} *
        a: Смена пароля от приложения возможна несколькими способами:
            1. на экране "Профиль" выберите "Изменить код входа в приложение".
            2. введите SMS-код.
            3. придумайте новый код для входа в приложение и повторите его.
        script:
            $response.replies = $response.replies || [];
            $response.replies.push({
                "type": "timeout",
                "interval": 2,
                "targetState": "/App/AppNoResponse"
            });
            
        state: AppNoResponse
            a: Либо нажмите на кнопку "Выйти" на странице ввода пароля для входа в приложение.
                После чего нужно будет заново пройти регистрацию:
                1. ввести полный номер карты (если оформляли ранее, иначе номер телефона и дату рождения),
                2. указать код из смс-код,
                3. придумать новый пароль для входа.
            script:
                $response.replies = $response.replies || [];
                $response.replies.push({
                    "type": "timeout",
                    "interval": 2,
                    "targetState": "/Bye"
                });
    
    state: Card
        q: [$oneWord] (2*|втор*|два*|карт*) [$oneWord] || fromState = "/Specify"
        q: * {($password|qr-код*|(кьюар*|куар*|qr) [код*]) * (покуп*|плат*|оплат*|оплач*|заплат*|зачисл*|купит*|счёт*|счет*|перевод*|перевест*)} *
        q: * {$password * карт*} *
        q: * {($change|$lost|$restore|$create|$err|проблем*) * $password * карт*} *
        q: * {{($change|$lost|$restore|$create|$err|проблем*) * $password * карт*} * [$app]} *
        q: * {{($change|$lost|$restore|$create|$err|проблем*) * [$app] * $password} * карт*} *
        q: * {{[как*|где] * ($findOut|ввест*|ввод*|запис*|запиш*|выда*|получ*|провер*|управл*|сохран*|хранит*|хранят*)} * $password * карт*} *
        a: Это можно сделать в приложении:
            1. На экране "Мои деньги" в разделе "Карты" нажмите на нужную.
            2. Выберите вкладку "Настройки".
            3. Нажмите "Сменить пин-код".
            4. И введите комбинацию, удобную вам.
            5. Повторите ее.
        go!: /Card/CardContinuation
            
        state: CardContinuation
            a: И все готово!
                Пин-код установлен, можно пользоваться.
            go!: /Bye
            
    state: Bye
        script:
            $jsapi.stopSession()
        a: Приятно было пообщаться. Всегда готов помочь вам снова.

    state: timeLimit
        event: timeLimit || fromState = "/Specify"
        script:
            $jsapi.stopSession()
